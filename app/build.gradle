apply plugin: 'com.android.application'

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    compileSdkVersion 27
    defaultConfig {
        applicationId "com.xplor.android"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        task parseXml() {
            def parsedProjectXml = (new XmlParser()).parse("app/src/main/res/values/attrs_font_drawable.xml")
            def fontDrawables = parsedProjectXml.depthFirst().findAll { it.name() == 'enum' }
            fontDrawables.each { fontDrawable ->
                buildConfigField "char", fontDrawable.@name.toUpperCase(), "'\\ue" + fontDrawable.@value + "'"
            }
        }
    }
    signingConfigs {
        release {
            if (rootProject.file('release.properties').exists()) {
                def props = new Properties()
                props.load(rootProject.file('release.properties').newDataInputStream())
                release {
                    storeFile rootProject.file(props.storeFile)
                    storePassword props.storePassword
                    keyAlias props.keyAlias
                    keyPassword props.keyPassword
                }
            }
        }
    }
    buildTypes {
        debug {
            buildConfigField "String", "BASE_URL", '"http://xplor.mixedquantum.com"'
        }
        debugRelease {
            initWith debug
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            signingConfig signingConfigs.release
            buildConfigField "String", "BASE_URL", '"http://xplor.mixedquantum.com"'
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets.debugRelease.setRoot('src/debug')

    sourceSets {
        debugRelease {
            java.srcDirs = ['src/debug/java', 'src/debugRelease/java']
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${variant.name}-${versionName}.apk"
        }
        if (variant.getBuildType().isMinifyEnabled()) {
            variant.assemble.doLast {
                copy {
                    from variant.mappingFile
                    into variant.mappingFile.parent
                    rename { String fileName ->
                        "mapping-${variant.name}-${version}.txt"
                    }
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.android.support:cardview-v7:27.0.2'
    implementation 'com.android.support:design:27.0.2'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.google.maps:google-maps-services:0.2.6'
    implementation 'org.slf4j:slf4j-nop:1.7.25'
    implementation 'com.google.android.gms:play-services-maps:11.8.0'
    implementation 'com.google.android.gms:play-services-places:11.8.0'
    implementation 'com.google.android.gms:play-services-location:11.8.0'
    implementation 'com.facebook.android:facebook-login:4.30.0'
    implementation "com.squareup.picasso:picasso:2.5.2"
    implementation "com.squareup.retrofit2:retrofit:2.3.0"
    implementation "com.squareup.okhttp3:logging-interceptor:3.8.0"
    implementation "com.squareup.retrofit2:converter-gson:2.3.0"
    implementation "com.jakewharton:butterknife:8.8.1"
    implementation "com.github.franmontiel:PersistentCookieJar:v1.0.1"
    implementation "org.greenrobot:eventbus:3.1.1"
    implementation 'de.hdodenhof:circleimageview:2.2.0'
    implementation 'com.github.ome450901:SimpleRatingBar:1.4.1'
    implementation 'com.github.lzyzsd:circleprogress:1.2.1'
    testImplementation 'junit:junit:4.12'
    annotationProcessor "com.jakewharton:butterknife-compiler:8.8.1"
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}
